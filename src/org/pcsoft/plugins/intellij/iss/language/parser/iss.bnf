{
    parserClass="org.pcsoft.plugins.intellij.iss.language.parser.IssParser"
    parserUtilClass="org.pcsoft.plugins.intellij.iss.language.parser.IssParserUtil"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Iss"
    psiImplClassSuffix="Impl"
    psiPackage="org.pcsoft.plugins.intellij.iss.language.parser.psi.element"
    psiImplPackage="org.pcsoft.plugins.intellij.iss.language.parser.psi.element"

    elementTypeHolderClass="org.pcsoft.plugins.intellij.iss.language.parser.IssGenTypes"
    elementTypeClass="org.pcsoft.plugins.intellij.iss.language.parser.psi.IssElementType"
    tokenTypeClass="org.pcsoft.plugins.intellij.iss.language.parser.psi.IssTokenType"

    psiImplUtilClass="org.pcsoft.plugins.intellij.iss.language.parser.psi.IssPsiUtils"
}

issFile ::= section*

/* Sections */
section ::= sectionTitle sectionContent
{
    methods=[getPresentation getName getSectionType]
}
sectionTitle ::= '[' NAME ']' EOL
{
    methods=[getName]
}
sectionContent ::= sectionLine*
sectionLine ::= singleProperty | multiProperty

singleProperty ::= singleElement EOL
multiProperty ::= (multiElement ';')* multiElement EOL

/* Elements */
singleElement ::= key '=' singleValue
multiElement::= key ':' multiValue

key ::= NAME
singleValue ::= (constValue | NUMBER | NAME | TEXT)*
multiValue ::= (refValue | NUMBER | stringValue | constValue)

/* Types */
refValue ::= NAME
stringValue ::= '"' (constValue | STRING)* '"'
constValue ::= '{' NAME '}'
